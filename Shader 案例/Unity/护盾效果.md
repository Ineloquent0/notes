
参考：https://www.bilibili.com/video/BV1T14y1i7cu

# 1. 模型部分

## 1.1 模型

1. 新建棱角球

![新建棱角球](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914110001.jpg)

2. 添加修改器：选择倒角 -> 选择顶点 -> 宽度类型设置为百分比 -> 宽度百分比设置为 33.33%
应用修改器

![添加修改器](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914110340.jpg)

3. 切换到编辑模式，拆分面：网格 -> 拆分 -> 沿边拆面/按顶点拆分面&边

![拆分面](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914110902.jpg)

4. 按住快捷键 `>.` 变换轴心点，选择各自的原点，然后进行缩放，缩放至合适的大小

![变换轴心点](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914111902.jpg)

![模型最终效果](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914112244.jpg)

## 1.2 UV

#### 第一套 UV

1. 切换到 UV 编辑视图，选择 UV -> 重置，得到效果如图

![UV 重置](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914112456.jpg)

2. 将五边形和六边形分开，调整至合适位置（选择模式选择点线面，方便选择）

![将 UV 调整至合适位置](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914112712.jpg)

3. 导出 UV ：在 UV 编辑模式下选择 UV -> 导出 UV 布局图

![导出 UV 布局图](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914113326.jpg)


#### 第二套 UV

1. 添加 UV 贴图：在属性视图中选择 数据 -> UV 贴图 -> 添加

![添加 UV 贴图](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914114529.jpg)

2. 选择第二套 UV，在编辑模式中选择 UV -> 柱面投影

![柱面投影](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914115430.jpg)

3. 在 UV 编辑视图中选择以各自原点缩放，缩放至最小，然后调整至合适位置
主要是为了做扫光效果

![20240914115926](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914115926.jpg)

## 1.3 贴图

#### PS 部分
将导出的 uv 导入 PS 
1. 设置背景色为黑色
2. 选择 uv 贴图图层，将透明度设为 0
3. 双击图层添加样式，选择内发光，调整好参数
4. 导出贴图

![导出贴图](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914113820.jpg)

#### Ds 部分
将贴图导入 Ds
1. 导入贴图，将此贴图设置为最终贴图的 R 通道
    * 使用 RGBA Split 节点分离原图通道
    * 使用 RGBA Merge 节点合并通道
2. 制作扫光效果，将扫光渐变设置为 G 通道
    * 使用 Gradient Linear 2 节点和 Historgram Scan 节点制作渐变
3. 使用 Perlin 噪声制作流光效果，将流光渐变设置为 B 通道
    * 使用 Perlin Noise 节点和 RT Caustics Grayscale 节点制作流光

![制作需要的贴图](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240914103406.jpg)

# 2. Unity 部分

## 2.1 顶点偏移

``` hlsl
Properties
{
    // 添加属性
    _Texture("Texture", 2D) = "white" {}
    _OffsetIntensity ("Offset Intensity", Range(0, 1)) = 0.1
    _OffsetSpeed("Offset Speed", Float) = 0.5
}

......

// 顶点着色器部分
v2f vert (appdata v)
{
    v2f o;

    // UV 滚动
    float2 uvOffset = float2(0, _Time.y * _OffsetSpeed) + v.uv1;
    // 纹理采样
    fixed3 texColor = tex2Dlod(_Texture, float4(uvOffset, 0, 0)).rgb;
    // 顶点偏移
    float3 offset = _OffsetIntensity * texColor.g * v.normal;
    v.vertex.xyz += offset;

    o.vertex = UnityObjectToClipPos(v.vertex);
    return o;
}
```

效果：

![护盾顶点偏移效果](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240913201935.gif)

## 2.2 颜色

``` hlsl
Properties
{
    // 颜色部分属性
    _Color("Color", Color) = (1,1,1,0.1)
    [HDR] _OutlineColor("Outline Color", Color) = (0,0,0,1)
    [HDR] _SweepColor("Sweep Color", Color) = (0,0,0,1)
    [HDR] _EmissionColor("Emission Color", Color) = (0,0,0,1)
    _EMissionSpeed("Emission Speed", Range(0, 5)) = 0.5
    _FresnelScale("Fresnel Scale", Range(0, 1)) = 0.1
}

......

// 片元着色器
fixed4 frag (v2f i) : SV_Target
{
    // 六边形轮廓部分
    half4 outlineColor = _OutlineColor * tex2D(_Texture, i.uv.xy).r;
    
    // 扫光部分（顶点偏移的部分）
    float2 sweepOffset = float2(0, _Time.y * _OffsetSpeed) + + i.uv.zw;
    half4 sweepColor = tex2D(_Texture, sweepOffset).g * _SweepColor;

    // 流光部分
    float2 emisOffset = float2(0, _Time.y * _EMissionSpeed) + i.uv.zw;
    half4 texColor = tex2D(_Texture, emisOffset).b * _EmissionColor;

    half4 color = outlineColor + sweepColor + texColor + _Color;

    // 菲涅尔 （abs 是为了让背面和正面的颜色一致，否则背面会过亮）
    fixed fresnel = pow(1 - abs(dot(i.worldNormal, i.worldViewDir)), 3);
    fresnel =  _FresnelScale + (1 - _FresnelScale) * fresnel;

    color.a = saturate(color.a);
    color *= fresnel;

    return color;
}
```

效果：

![护盾颜色效果](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240913202123.gif)


## 2.3 护盾展开效果
可直接使用第二套 uv 的 v 坐标进行裁剪

``` hlsl
Properties
{
    _Cutoff("Cutoff", Range(0, 1)) = 0.5
}
......

fixed4 frag (v2f i) : SV_Target
{
    // 护盾展开的效果（使用第二套 UV 进行裁剪）
    clip(_Cutoff - 1 + i.uv.w);
    ......
}
```

效果：

![护盾展开效果](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240913204623.gif)

### 护盾展开边缘溶解效果

``` hlsl
Properties
{
    // 护盾展开边缘溶解部分
    [Header(Dissolve Part)]
    _Cutoff("Cutoff", Range(0, 1)) = 0.5
    _DissolveWidth("Dissolve Width", Range(0, 1)) = 0.5
    [HDR]_DissolveColor("Dissolve Color", Color) = (0,0,0,1)
}

......

//计算可以驱动整块多边形进行顶点动画的方法
float3 VertexToFaceCenterFirst (float3 worldPos,float3 normalWS)
{
    //利用模型中心点，顶点法线 等数据算出顶点到多边形面中心点的向量，以此来移动顶点到模型法向量的中心点位置 
    //从而使得多边形的顶点能够统一朝一个方向变化
    float3 centerPositionWS = mul(unity_ObjectToWorld, float4(0,0,0,1)).xyz;
    float3 comparePositionWS = worldPos - centerPositionWS;
    //Rejection 算法
    float3 newPos = comparePositionWS - normalWS * dot(comparePositionWS, normalWS) / dot(normalWS, normalWS);  
    return newPos;
}

v2f vert (appdata v)
{
    // 护盾展开边缘缩放效果
    float3 centerPos = VertexToFaceCenterFirst (worldPos,o.worldNormal);
    float scale = 1 - saturate(- (1 - _Cutoff - v.uv1.y) / _DissolveWidth);
    scale *= scale;         // 线性变化有些许单调，添加一点曲线变化
    o.colorInfo.z = scale;  // z 存储溶解部分的缩放因子
    scale *= 0.5;           // 从 0-1 映射到 0-0.5
    v.vertex.xyz += centerPos * - scale;
    ......
}

fixed4 frag (v2f i) : SV_Target
{
    // 护盾展开的效果（使用第二套 UV 进行裁剪）
    fixed cutoff = - (1 - _Cutoff - i.uv.w);
    clip(cutoff);

    half4 dissolveColor = _DissolveColor * i.colorInfo.z;
    ......
}
```

效果：

![护盾展开边缘溶解效果](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240916200251.gif)

## 2.4 边缘交互部分

``` hlsl
Properties
{
    // 边缘交互部分属性
    _BorderWidth("Border Width", Range( 0 , 1)) = 0
    [HDR]_BorderColor("Border Color", Color) = (0,0,0,0)
}

......

fixed _BorderWidth;
half4 _BorderColor;

// 深度纹理
sampler2D  _CameraDepthTexture;

v2f vert (appdata v)
{
    ......

    // 获取屏幕坐标
    o.scrPos = ComputeScreenPos(o.vertex);
    COMPUTE_EYEDEPTH(o.eyeDepth);
}

fixed4 frag (v2f i) : SV_Target
{
    ......
    
    // 边缘交互部分
    float screenDepth = LinearEyeDepth(SAMPLE_DEPTH_TEXTURE_PROJ(_CameraDepthTexture, UNITY_PROJ_COORD(i.scrPos)));
    float interesect = 1 - saturate((screenDepth - i.eyeDepth) / _BorderWidth);
    color += _BorderColor * interesect;
}
```

效果：

![护盾边缘交互效果](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240913225206.gif)

## 2.5 护盾受击效果
参考：
https://blog.csdn.net/liweizhao/article/details/132178610
https://blog.csdn.net/ezcome11/article/details/118918951

之前贴图 a 通道是空的，我们可以添加一套渐变纹理到贴图的 a 通道，用来做受击的扩散效果：

![渐变纹理](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240915163125.jpg)

### Shader 部分
``` hlsl
Properties
{
    // 受击部分属性
    [HDR] _HitColor("Hit Color", Color) = (0,0,0,1)
    _WaveWidth("Wave Width", Range(0, 1)) = 0.5
    _HitFadeDistance("Hit Fade Distance", Float) = 1
    _HitOffsetIntensity ("Hit Offset Intensity", Range(0, 1)) = 0.1
}

......


// 护盾受击部分
// 受击信息，通过脚本传递（xyz 为受击位置，w 为扩散大小，在脚本中修改大小）
float4 _HitInfo[10];

half4 _HitColor;
half _WaveWidth;
half _HitFadeDistance;
half _HitOffsetIntensity;

//计算可以驱动整块多边形进行顶点动画的方法
float3 VertexToFaceCenterFirst (float3 worldPos,float3 normalWS)
{
    //利用模型中心点，顶点法线 等数据算出顶点到多边形面中心点的向量，以此来移动顶点到模型法向量的中心点位置 
    //从而使得多边形的顶点能够统一朝一个方向变化
    float3 centerPositionWS = mul(unity_ObjectToWorld, float4(0,0,0,1)).xyz;
    float3 comparePositionWS = worldPos - centerPositionWS;
    //Rejection 算法
    float3 newPos = comparePositionWS - normalWS * dot(comparePositionWS, normalWS) / dot(normalWS, normalWS);  
    return newPos;
}

float3 VertexToFaceCenter (float3 worldPos,float3 normalWS)
{
    //利用模型中心点，顶点法线 等数据算出顶点到多边形面中心点的向量，以此来移动顶点到模型法向量的中心点位置 
    //从而使得多边形的顶点能够统一朝一个方向变化
    float3 temp = VertexToFaceCenterFirst (worldPos,normalWS);
    float3 newVertexPosition = worldPos.xyz -temp;
    return newVertexPosition;
}

// 计算护盾受击部分的扩散效果
float HitWaveFunc (sampler2D RampTex, float3 worldPos, float3 waveWidth, float hitFadeDistance)
{
    float hitInfo = 0;
    for (int j = 0; j < 10; j++)
    {
        if(_HitInfo[j].w == 0) break;

        float3 hitPos = _HitInfo[j].xyz;
        float3 hitSize = _HitInfo[j].w;
        float hitDistance = distance(hitPos, worldPos);
        float hitRange = saturate((hitSize - hitDistance) / waveWidth);
        float hitWave = tex2Dlod(RampTex, float4(hitRange, 0.5, 0, 0)).a;
        float hitFade = saturate(1 - (hitDistance / hitFadeDistance));
        hitInfo += hitWave * hitFade;
    }
    return hitInfo;
}


v2f vert (appdata v)
{
    v2f o;

    o.worldNormal = normalize(UnityObjectToWorldNormal(v.normal));
    float3 worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;

    // 护盾受击部分
    // 算出中心位置
    float3 newPos = VertexToFaceCenter (worldPos,o.worldNormal);
    // 计算护盾受击部分的扩散效果
    float hitInfo = HitWaveFunc(_Texture, newPos, _WaveWidth, _HitFadeDistance);
    float3 hitOffset = _OffsetIntensity * hitInfo * v.normal;

    v.vertex.xyz += hitOffset;
    o.vertexOffset.y = hitInfo;

    ......
}

```

### C# 部分

``` csharp
using System.Collections;
using System.Collections.Generic;
using UnityEngine;

/// <summary>
/// 护盾受击效果
/// </summary>
public class ShieldHitWave : MonoBehaviour
{
    public string triggerTag = "HitObj";

    // 动画部分
    public float startSize = 0f;
    public float endSize = 5.0f;
    // 动画持续时间
    public float duration = 3.0f;
    // Size 的动画曲线
    public AnimationCurve sizeCurve = AnimationCurve.Linear(0, 0, 1, 1);

    // 受击位置和对应特效的大小 （xyz为位置，w为大小）
    private Vector4[] hitInfo;
    // size 对应的曲线
    private float[] curveValues;

    // 当前数组大小
    private int arrSize = 0;

    void Start () {
        hitInfo = new Vector4[10];
        curveValues = new float[10];
        arrSize = 0;
	}


	void Update ()
    {
        // 射线检测
        if (Input.GetMouseButtonDown(0))
        {
            RaycastHit raycastHit;
            Ray ray = Camera.main.ScreenPointToRay(Input.mousePosition);

            if (Physics.Raycast(ray,out raycastHit, 1000))
            {
                if (arrSize < hitInfo.Length && raycastHit.transform.CompareTag(triggerTag))
                {
                    hitInfo[arrSize] = raycastHit.point;
                    hitInfo[arrSize].w = startSize;
                    curveValues[arrSize] = 0.0f;
                    arrSize++;
                    
                    Shader.SetGlobalVectorArray("_HitInfo", hitInfo);
                    // StartAnim(arrSize - 1);
                }
            }
        }

        // 动画部分
        for (int i = 0; i < arrSize; i++){
            float curve = curveValues[i] + Time.deltaTime / duration;
            if(curve <= 1.0f)
            {
                curveValues[i] = curve;
                curve = sizeCurve.Evaluate(curve);
                float size = Mathf.Lerp(startSize, endSize, curve);
                hitInfo[i].w = size;
                Shader.SetGlobalVectorArray("_HitInfo", hitInfo);
            }
            else
            {
                RemoveAt(i);
                i--;
            }
        }

    }

    // 移除对应下标的元素
    private void RemoveAt(int index)
    {
        for (int i = index; i < arrSize; i++)
        {
            if (i == arrSize - 1)
            {
                hitInfo[i] = Vector4.zero;
                curveValues[i] = 0.0f;
            }
            else
            {
                hitInfo[i] = hitInfo[i + 1];
                curveValues[i] = curveValues[i + 1];
            }
        }
        arrSize--;
        Shader.SetGlobalVectorArray("_HitInfo", hitInfo);
    }
}

```

### 效果

![受击效果](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240915164515.gif)



## 完整代码

### 不含受击效果的 Shader
``` hlsl
Shader "Custom/Hexagonal Shield"
{
    Properties
    {
		_Texture("Texture", 2D) = "white" {}
        // 顶点偏移
        _OffsetIntensity ("Offset Intensity", Range(0, 1)) = 0.1
		_OffsetSpeed("Offset Speed", Float) = 0.5
        
        // 颜色部分
		_Color("Color", Color) = (1,1,1,0.1)
        [HDR] _OutlineColor("Outline Color", Color) = (0,0,0,1)
        [HDR] _SweepColor("Sweep Color", Color) = (0,0,0,1)
        [HDR] _EmissionColor("Emission Color", Color) = (0,0,0,1)
        _EMissionSpeed("Emission Speed", Range(0, 5)) = 0.5
        _FresnelScale("Fresnel Scale", Range(0, 1)) = 0.1

        _Cutoff("Cutoff", Range(0, 1)) = 0.5

        // 边缘交互部分
		_BorderWidth("Border Width", Range( 0 , 1)) = 0
		[HDR]_BorderColor("Border Color", Color) = (0,0,0,0)
    }
    SubShader
    {
        Tags { "RenderType"="Transparent" "IgnoreProjector"="True" "Queue"="Transparent" }
        LOD 100

        Pass
        {
            Blend SrcAlpha OneMinusSrcAlpha
            ZWrite Off
            Cull Off
            
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float2 uv : TEXCOORD0;
                float2 uv1 : TEXCOORD1;
            };

            struct v2f
            {
                float4 uv : TEXCOORD0;// 存储两套 UV
                float4 vertex : SV_POSITION;
                fixed3 worldNormal : TEXCOORD1;
                fixed3 worldViewDir : TEXCOORD2;
                float4 scrPos : TEXCOORD3;
                float eyeDepth : TEXCOORD4;
            };

            sampler2D _Texture;
            fixed4 _Color;
            half _OffsetIntensity;
            half _OffsetSpeed;

            half4 _OutlineColor;
            half4 _SweepColor;
            half4 _EmissionColor;
            half _EMissionSpeed;
            half _FresnelScale;

            fixed _Cutoff;

            fixed _BorderWidth;
            half4 _BorderColor;

            // 深度纹理
            sampler2D  _CameraDepthTexture;

            v2f vert (appdata v)
            {
                v2f o;

                // UV 滚动
                float2 uvOffset = float2(0, _Time.y * _OffsetSpeed) + v.uv1;
                // 纹理采样
                fixed3 texColor = tex2Dlod(_Texture, float4(uvOffset, 0, 0)).rgb;
                // 顶点偏移
                float3 offset = _OffsetIntensity * texColor.g * v.normal;
                v.vertex.xyz += offset;

                o.vertex = UnityObjectToClipPos(v.vertex);
                o.worldNormal = normalize(UnityObjectToWorldNormal(v.normal));
                float3 worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                o.worldViewDir = normalize(UnityWorldSpaceViewDir(worldPos));

                o.uv.xy = v.uv;
                o.uv.zw = v.uv1;

                // 获取屏幕坐标
                o.scrPos = ComputeScreenPos(o.vertex);
                COMPUTE_EYEDEPTH(o.eyeDepth);

                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                // 护盾展开的效果（使用第二套 UV 进行裁剪）
                clip(_Cutoff - 1 + i.uv.w);

                // 六边形轮廓部分
                half4 outlineColor = _OutlineColor * tex2D(_Texture, i.uv.xy).r;
                
                // 扫光部分（顶点偏移的部分）
                float2 sweepOffset = float2(0, _Time.y * _OffsetSpeed) + + i.uv.zw;
                half4 sweepColor = tex2D(_Texture, sweepOffset).g * _SweepColor;

                // 流光部分
                float2 emisOffset = float2(0, _Time.y * _EMissionSpeed) + i.uv.zw;
                half4 texColor = tex2D(_Texture, emisOffset).b * _EmissionColor;

                half4 color = outlineColor + sweepColor + texColor + _Color;

                // 菲涅尔 （abs 是为了让背面和正面的颜色一致，否则背面会过亮）
                fixed fresnel = pow(1 - abs(dot(i.worldNormal, i.worldViewDir)), 3);
                fresnel =  _FresnelScale + (1 - _FresnelScale) * fresnel;

                color.a = saturate(color.a);
                color *= fresnel;

                // 边缘交互部分
                float screenDepth = LinearEyeDepth(SAMPLE_DEPTH_TEXTURE_PROJ(_CameraDepthTexture, UNITY_PROJ_COORD(i.scrPos)));
                float interesect = 1 - saturate((screenDepth - i.eyeDepth) / _BorderWidth);
                color += _BorderColor * interesect;

                return color;
            }
            ENDCG
        }
    }
}
```

### 包含受击效果的 Shader
``` hlsl
Shader "Custom/Hexagonal Shield"
{
    Properties
    {
		_Texture("Texture", 2D) = "white" {}

        // 扫光部分
        [Header(Sweep Part)]
        [Toggle(_USESWEEP)] _UseSweep("Use Sweep", Int) = 1
        _OffsetIntensity ("Offset Intensity", Range(0, 1)) = 0.1
		_OffsetSpeed("Offset Speed", Float) = 0.5
        [HDR] _SweepColor("Sweep Color", Color) = (0,0,0,1)
        
        // 颜色部分
        [Header(Color Part)]
		_Color("Color", Color) = (1,1,1,0.1)
        [HDR] _OutlineColor("Outline Color", Color) = (0,0,0,1)
        [HDR] _EmissionColor("Emission Color", Color) = (0,0,0,1)
        _EMissionSpeed("Emission Speed", Range(0, 5)) = 0.5
        _FresnelScale("Fresnel Scale", Range(0, 1)) = 0.1

        // 护盾展开边缘溶解部分
        [Header(Dissolve Part)]
        _Cutoff("Cutoff", Range(0, 1.2)) = 0.5
        _DissolveWidth("Dissolve Width", Range(0, 0.5)) = 0.2
        [HDR]_DissolveColor("Dissolve Color", Color) = (0,0,0,1)

        // 边缘交互部分
        [Header(Border Interact Part)]
		_BorderWidth("Border Width", Range( 0 , 1)) = 0
		[HDR]_BorderColor("Border Color", Color) = (0,0,0,0)

        // 护盾受击部分
        [Header(Hit Part)]
        [HDR] _HitColor("Hit Color", Color) = (0,0,0,1)
        _WaveWidth("Wave Width", Range(0, 1)) = 0.5
        _HitFadeDistance("Hit Fade Distance", Float) = 1
        _HitOffsetIntensity ("Hit Offset Intensity", Range(0, 1)) = 0.1
    }
    SubShader
    {
        Tags { "RenderType"="Transparent" "IgnoreProjector"="True" "Queue"="Transparent" }
        LOD 100

        Pass
        {
            Blend SrcAlpha OneMinusSrcAlpha
            ZWrite Off
            Cull Off
            
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            // 定义 Toggle
            #pragma shader_feature _USESWEEP

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float2 uv : TEXCOORD0;
                float2 uv1 : TEXCOORD1;
            };

            struct v2f
            {
                float4 uv : TEXCOORD0;// 存储两套 UV
                float4 vertex : SV_POSITION;
                fixed3 worldNormal : TEXCOORD1;
                fixed3 worldViewDir : TEXCOORD2;
                float4 scrPos : TEXCOORD3;
                float eyeDepth : TEXCOORD4;
                // 顶点着色器输出到片元着色器的颜色信息 （x为扫光强度，y为护盾受击部分，z为护盾展开边缘溶解部分）
                fixed3 colorInfo : TEXCOORD5;
            };

            sampler2D _Texture;
            // 顶点偏移
            half _OffsetIntensity;
            half _OffsetSpeed;
            
            // 颜色部分
            fixed4 _Color;
            half4 _OutlineColor;
            half4 _SweepColor;
            half4 _EmissionColor;
            half _EMissionSpeed;
            half _FresnelScale;

            fixed _Cutoff;
            fixed _DissolveWidth;
            half4 _DissolveColor;

            // 边缘交互部分
            fixed _BorderWidth;
            half4 _BorderColor;

            // 护盾受击部分
            // 受击信息，通过脚本传递（xyz 为受击位置，w 为扩散大小，在脚本中修改大小）
            float4 _HitInfo[10];

            half4 _HitColor;
            half _WaveWidth;
            half _HitFadeDistance;
            half _HitOffsetIntensity;

            // 深度纹理
            sampler2D  _CameraDepthTexture;

            //计算可以驱动整块多边形进行顶点动画的方法
            float3 VertexToFaceCenterFirst (float3 worldPos,float3 normalWS)
            {
                //利用模型中心点，顶点法线 等数据算出顶点到多边形面中心点的向量，以此来移动顶点到模型法向量的中心点位置 
                //从而使得多边形的顶点能够统一朝一个方向变化
                float3 centerPositionWS = mul(unity_ObjectToWorld, float4(0,0,0,1)).xyz;
                float3 comparePositionWS = worldPos - centerPositionWS;
                //Rejection 算法
                float3 newPos = comparePositionWS - normalWS * dot(comparePositionWS, normalWS) / dot(normalWS, normalWS);  
                return newPos;
            }

            float3 VertexToFaceCenter (float3 worldPos,float3 normalWS)
            {
                //利用模型中心点，顶点法线 等数据算出顶点到多边形面中心点的向量，以此来移动顶点到模型法向量的中心点位置 
                //从而使得多边形的顶点能够统一朝一个方向变化
                float3 temp = VertexToFaceCenterFirst (worldPos,normalWS);
                float3 newVertexPosition = worldPos.xyz -temp;
                return newVertexPosition;
            }

            // 计算护盾受击部分的扩散效果
            float HitWaveFunc (sampler2D RampTex, float3 worldPos, float3 waveWidth, float hitFadeDistance)
            {
                float hitInfo = 0;
                for (int j = 0; j < 10; j++)
                {
                    if(_HitInfo[j].w == 0) break;

                    float3 hitPos = _HitInfo[j].xyz;
                    float3 hitSize = _HitInfo[j].w;
                    float hitDistance = distance(hitPos, worldPos);
                    float hitRange = saturate((hitSize - hitDistance) / waveWidth);
                    float hitWave = tex2Dlod(RampTex, float4(hitRange, 0.5, 0, 0)).a;
                    float hitFade = saturate(1 - (hitDistance / hitFadeDistance));
                    hitInfo += hitWave * hitFade;
                }
                return hitInfo;
            }

            v2f vert (appdata v)
            {
                v2f o = (v2f)0;

            #if _USESWEEP
                // 护盾扫光部分
                float2 uvOffset = float2(0, _Time.y * _OffsetSpeed) + v.uv1;
                // 纹理采样
                fixed texColor = tex2Dlod(_Texture, float4(uvOffset, 0, 0)).g;
                // 顶点偏移
                float3 offset = _OffsetIntensity * texColor * v.normal;
            #endif

            
                o.worldNormal = normalize(UnityObjectToWorldNormal(v.normal));
                float3 worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;

                // 护盾展开边缘缩放效果
                float3 centerPos = VertexToFaceCenterFirst (worldPos,o.worldNormal);
                float scale = 1 - saturate(- (1 - _Cutoff - v.uv1.y) / _DissolveWidth);
                scale *= scale;         // 线性变化有些许单调，添加一点曲线变化
                o.colorInfo.z = scale;  // z 存储溶解部分的缩放因子
                scale *= 0.5;           // 从 0-1 映射到 0-0.5
                v.vertex.xyz += centerPos * - scale;

                
                // 护盾受击部分
                // 算出中心位置
                float3 newPos = VertexToFaceCenter (worldPos,o.worldNormal);
                // 计算护盾受击部分的扩散效果
                float hitInfo = HitWaveFunc(_Texture, newPos, _WaveWidth, _HitFadeDistance);
                float3 hitOffset = _OffsetIntensity * hitInfo * v.normal;

            #if _USESWEEP
                v.vertex.xyz += max(offset, hitOffset);
                o.colorInfo.x = texColor;
            #else
                v.vertex.xyz += hitOffset;
            #endif
                o.colorInfo.y = hitInfo;


                o.vertex = UnityObjectToClipPos(v.vertex);
                worldPos = mul(unity_ObjectToWorld, v.vertex).xyz;
                o.worldViewDir = normalize(UnityWorldSpaceViewDir(worldPos));

                o.uv.xy = v.uv;
                o.uv.zw = v.uv1;

                // 获取屏幕坐标
                o.scrPos = ComputeScreenPos(o.vertex);
                COMPUTE_EYEDEPTH(o.eyeDepth);

                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                // 护盾展开的效果（使用第二套 UV 进行裁剪）
                fixed cutoff = - (1 - _Cutoff - i.uv.w);
                clip(cutoff);

                half4 dissolveColor = _DissolveColor * i.colorInfo.z;

                // 六边形轮廓部分
                half4 outlineColor = _OutlineColor * tex2D(_Texture, i.uv.xy).r;
                
                // 扫光部分（顶点偏移的部分）
                half4 sweepColor = i.colorInfo.x * _SweepColor;
                // 护盾受击部分
                half4 hitColor = i.colorInfo.y * _HitColor;

                // 流光部分
                float2 emisOffset = float2(0, _Time.y * _EMissionSpeed) + i.uv.zw;
                half4 texColor = tex2D(_Texture, emisOffset).b * _EmissionColor;

                half4 color = outlineColor + sweepColor + hitColor + texColor + _Color + dissolveColor;

                // 菲涅尔 （abs 是为了让背面和正面的颜色一致，否则背面会过亮）
                fixed fresnel = pow(1 - abs(dot(i.worldNormal, i.worldViewDir)), 3);
                fresnel =  _FresnelScale + (1 - _FresnelScale) * fresnel;

                color.a = saturate(color.a);
                color *= fresnel;

                // 边缘交互部分
                float screenDepth = LinearEyeDepth(SAMPLE_DEPTH_TEXTURE_PROJ(_CameraDepthTexture, UNITY_PROJ_COORD(i.scrPos)));
                float interesect = 1 - saturate((screenDepth - i.eyeDepth) / _BorderWidth);
                color += _BorderColor * interesect;

                return color;
            }
            ENDCG
        }
    }
}

```