
# 第5章 开始 Unity Shader 学习之旅

## 5.1 本书使用的软件和环境
本书使用的Unity版本是Unity 5.2.1免费版。

本书工程编写的系统环境是Mac OS X 10.9.5。如果读者使用的是其他系统，绝大部分情况也不会有任何问题。但有时会由于图像编程接口的种类和版本不同而有一些差别，这是因为Mac使用的图像编程接口是基于OpenGL的，而其他平台如Windows，可能使用的是DirectX。例如，在OpenGL中，渲染纹理（Render Texture）的(0, 0)点是在左下角，而在DirectX中，(0, 0)点是在左上角。



## 5.2 一个最简单的顶点/片元着色器

### 5.2.1 顶点/片元着色器的基本结构

我们在 3.3 节已经看到了 Unity Shader 的基本结构。它包含了Shader、Properties、SubShader、Fallback 等语义块。顶点/片元着色器的结构与之大体类似，它的结构如下:

```
Shader "MyShaderName" {

    Properties {
        // 属性
    }

    SubShader {
        // 针对显卡 A 的 SubShader
        Pass {
            // 设置渲染状态和标签

            // 开始 Cg 代码片段
            CGPROGRAM
            // 该代码片段的编译指令，例如：
            #pragma vertex vert
            #pragma fragment frag
            
            // Cg 代码写在这里

            ENDCG

            // 其他设置

        }
        //其他需要的 Pass
    }
    SubShader {
        // 针对显卡 B 的 SubShader
    }

    // 上述 SubShader 都失败后用于回调的 Unity Shader
    Fallback "VertexLit"
}
```

其中，最重要的部分是Pass语义块，我们绝大部分的代码都是写在这个语义块里面的。下面我们就来创建一个最简单的顶点/片元着色器：
1. 新建一个场景，把它命名为Scene_5_2。去掉天空盒，在菜单栏中选择 Window > Lighting > Skybox 把该项设置为None。(Unity 2023 是 Window > Rendering > Lighting > Environment > Skybox Material)
2. 新建一个 Unity Shader，把它命名为 Chapter5-SimpleShader。
3. 新建一个材质，把它命名为 SimpleShaderMat。把第2步中新建的 Unity Shader 赋给它。
4. 新建一个球体，拖曳它的位置以便在Game视图中可以合适地显示出来。把第3步中新建的材质拖曳给它。
5. 双击打开第2步中创建的 Unity Shader。删除里面的所有代码，把下面的代码粘贴进去:

```
Shader "Unity Shaders Book/Chapter 5/Simple Shader"{ 
    // Properties 语义并不是必需的

    SubShader { 
        Pass{
            CGPROGRAM

            // 告诉 Unity 哪个函数是顶点着色器，哪个函数是片元着色器
            // #pragma vertex name
            // #pragma fragment name
            #pragma vertex vert
            #pragma fragment frag

            // 顶点着色器函数
            float4 vert(float4 v : POSITION): SV_POSITION { 
                // 把顶点坐标从模型空间转换到裁剪空间
                // return mul (UNITY_MATRIX_MVP, v);
                return UnityObjectToClipPos (v);
            }

            // 片元着色器函数
            fixed4 frag(): SV_Target { 
                return fixed4(1.0, 1.0, 1.0, 1.0);
            }

            ENDCG
        }
    }
}
```

保存并返回 Unity 查看结果：

![图5.2 用一个最简单的顶点/片元着色器得到一个白色的球](https://raw.githubusercontent.com/Ineloquent0/notes/main/Shader/Unity%20Shader%20%E5%85%A5%E9%97%A8%E7%B2%BE%E8%A6%81/images/5.2.jpg)


我们具体看一下 vert 函数的定义：

```
float4 vert(float4 v : POSITION): SV_POSITION { 
    // return mul (UNITY_MATRIX_MVP, v);
    return UnityObjectToClipPos (v);
}
```

POSITION 和 SV_POSITION 是 Cg/HLSL 中的语义，这些语义将告诉系统需要哪些输入值，以及输出的是什么。
在这里 POSITION 将告诉 Unity 把模型的顶点坐标填充到输入参数 v 中，SV_POSITION 将告诉 Unity 顶点着色器的输出是裁剪空间中的顶点坐标。


然后，我们再来看一下 frag 函数：

```
// 片元着色器函数
fixed4 frag(): SV_Target { 
    return fixed4(1.0, 1.0, 1.0, 1.0);
}
```

在本例中，frag 函数没有任何输入。它的输出是一个 fixed4 类型的变量，并且使用了 SV_Target 语义进行限定。SV_Targer 也是 HLSL 中的一个系统语义，它等同于告诉渲染器，把用户的输出颜色存储到一个渲染目标(render target)中，这里将输出到默认的帧缓存中。片元着色器中的代码很简单，返回了一个表示白色的 fixed4 类型的变量。片元着色器输出的颜色的每个分量范围在[0, 1]，其中(0,0,0)表示黑色，而(1,1,1)表示白色。


### 5.2.2 模型数据从哪里来？

如果我们想要得到更多模型数据，我们就需要定义一个结构体作为输入参数。

```
Shader "Unity Shaders Book/Chapter 5/Simple Shader"{ 
    SubShader { 
        Pass{
            CGPROGRAM

            #pragma vertex vert
            #pragma fragment frag

            // 使用一个结构体来定义顶点着色器的输入
            struct a2v {
                // POSITION 语义告诉Unity，用模型空间的顶点坐标填充 vertex 变量
                float4 vertex : POSITION;
                // NORMAL 语义告诉Unity，用模型空间的法线方向填充 normal 变量
                float3 normal : NORMAL;
                // TEXCOORD0 语义告诉Unity，用模型的第一套纹理坐标填充 texcoord 变量
                float4 texcoord : TEXCOORD0;
            };

            float4 vert(a2v v): SV_POSITION { 
                // 使用 v.vertex 来访问模型空间的顶点坐标
                // return mul(UNITY_MATRIX_MVP, v.vertex);
                return UnityObjectToClipPos (v.vertex);
            }

            fixed4 frag(): SV_Target { 
                return fixed4(1.0, 1.0, 1.0, 1.0);
            }

            ENDCG
        }
    }
}
```

对于顶点着色器的输入，Unity 支持的语义有：
* POSITION: 顶点位置
* TANGENT：切线
* NORMAL：法线
* TEXCOORD0：纹理坐标
* TEXCOORD1
* TEXCOORD2
* TEXCOORD3
* COLOR：顶点颜色
等等...


### 5.2.3 顶点着色器和片元着色器之间如何通信

我们希望从顶点着色器输出一些数据到片元着色器，这就涉及到顶点着色器和片元着色器之间的通信。
为此，我们需要再定义一个新的结构体：

```
Shader "Unity Shaders Book/Chapter 5/Simple Shader"{ 
    SubShader { 
        Pass{
            CGPROGRAM

            #pragma vertex vert
            #pragma fragment frag

            struct a2v {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float4 texcoord : TEXCOORD0;
            };

            struct v2f {
                // SV_POSITION 语义告诉 Unity，pos 里包含了顶点在裁剪空间中的位置
                float4 pos : SV_POSITION;
                // COLOR0 语义可以用于存储颜色信息
                fixed3 color : COLOR0;
            };

            v2f vert(a2v v) { 
                // 声明输出结构
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                // v.normal 包含了顶点的法线方向，其分量范围在[-1, 1]
                // 下面代码把分量范围映射到了 [0, 1]
                // 存储到 o.color 中传递给片元着色器
                o.color =  v.normal * 0.5 + fixed3(0.5, 0.5, 0.5);
                return o;
            }

            fixed4 frag(v2f i): SV_Target { 
                // 将插值后的 i.color 显示到屏幕上
                return fixed4(i.color, 1.0);
            }

            ENDCG
        }
    }
}
```


### 5.2.4 如何使用属性

我们可以定义一些属性，让用户可以调整着色器的一些参数。

```
Shader "Unity Shaders Book/Chapter 5/Simple Shader"{ 
    Properties { 
        // 声明一个 Color 类型的属性
        _Color ("Color Tint", Color) = (1.0, 1.0, 1.0, 1.0)
    }
    SubShader { 
        Pass{
            CGPROGRAM

            #pragma vertex vert
            #pragma fragment frag

            // 在 CG 代码中，我们需要定义一个与属性名称和类型都匹配的变量
            fixed4 _Color;

            struct a2v {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
                float4 texcoord : TEXCOORD0;
            };

            struct v2f {
                float4 pos : SV_POSITION;
                fixed3 color : COLOR0;
            };

            v2f vert(a2v v) {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.color =  v.normal * 0.5 + fixed3(0.5, 0.5, 0.5);
                return o;
            }

            fixed4 frag(v2f i): SV_Target { 
                fixed3 c = i.color;
                // 使用 _Color 属性来控制输出颜色
                c *= _Color.rgb;
                return fixed4(c, 1.0);
            }

            ENDCG
        }
    }
}
```

ShaderLab 属性类型和 Cg 变量类型的匹配关系：

| ShaderLab 属性类型 | Cg 变量类型 |
| --- | --- |
| Color, Vector | float4, half4, fixed4 |
| Range, Float | float, half, fixed |
| 2D           | sampler2D |
| 3D           | sampler3D |
| Cube         | samplerCUBE |



## 5.3 Unity 提供的内置文件和变量
为了方便开发者的编码过程，Unity 提供了很多内置文件，这些文件包含了很多提前定义的函数、变量和宏等。如果读者在学习他人编写的 Unity Shader 代码时，遇到了一些从未见过的变量、函数，而又无法找到对应的声明和定义，那么很有可能就是这些代码使用了 Unity 内置文件提供的函数和变量。

### 5.3.1 内置的包含文件
**包含文件（include file）**，是类似于C++中头文件的一种文件。在Unity中，它们的文件后缀是．cginc。在编写Shader时，我们可以使用#include指令把这些文件包含进来，这样我们就可以使用Unity为我们提供的一些非常有用的变量和帮助函数。例如：

```
CGPROGRAM
// ...
#include "UnityCG.cginc"
// ...
ENDCG
```

那么，这些文件在哪里呢?我们可以在官方网站(https://unity3d.com/cn/get-unity/download/archive)上选择下载 > 内置着色器 来直接下载这些文件（2023年：https://unity.com/cn/releases/editor/archive 上选择 See all > Other installs > Shaders）。图5.3 显示了由官网压缩包得到的文件。

![图5.3 Unity 的内置着色器](https://raw.githubusercontent.com/Ineloquent0/notes/main/Shader/Unity%20Shader%20%E5%85%A5%E9%97%A8%E7%B2%BE%E8%A6%81/images/5.3.jpg)

我们也可以在Unity的安装目录下找到 CGIncludes文件夹，在Mac上位于：/Applications/Unity/Unity.app/Contents/CGIncludes；在Windows上位于：Unity的安装路径/Data/CGIncludes。

表5.2 Unity 中一些常用的包含文件：
 文件名 | 描述
 --- | ---
 UnityCG.cginc              | 包含了最常用的帮助函数、宏和结构体等
 UnityShaderVariables.cginc | 在编译 Unity Shader 时，会被自动包含进来。包含了许多内置的全局变量，如 UNITY_MATRIX_MVP 等
 Lighting.cginc             | 包含了各种内置的光照模型，如果编写的是 Surface Shader 的话，会自动包含进来
 HLSLSupport.cginc          | 在编译 Unity Shader 时，会被自动包含进来。声明了很多用于跨平台编译的宏和定义

表5.3 UnityCG.cginc 中一些常用的结构体
 名称 | 描述 | 包含的变量
 --- | --- | ---
 appdata_base   | 可用于顶点着色器的输入 | 顶点位置、顶点法线、第一组纹理坐标
 appdata_tan    | 可用于顶点着色器的输入 | 顶点位置、顶点切线、顶点法线、第一组纹理坐标
 appdata_full   | 可用于顶点着色器的输入 | 顶点位置、顶点切线、顶点法线、四组(或更多)纹理坐标
 appdata_img    | 可用于顶点着色器的输入 | 顶点位置、第一组纹理坐标
 v2f_img        | 可用于顶点着色器的输出 | 裁剪空间中的位置、第一组纹理坐标



