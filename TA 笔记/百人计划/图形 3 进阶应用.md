# 1. 模板测试和深度测试

## 1.1 模板测试 (Stencil Test)

![模板测试](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240928120606.jpg)

* 左图为颜色缓冲区中的一张图，在模板缓冲区中我们会给这张图的每一个片元分配一个0-255的数字（8位，默认为0）
* 中、右图可以看到，我们修改了一些0为1，通过自定义的一些准则，如输出模板缓冲区中1对应的片元的颜色；0的不输出，最后通过模板测试的结果就如右图所示

#### 模板测试的应用

<div align=center>
<img src="https://raw.githubusercontent.com/Ineloquent0/notes/main/images/a1bef728-9bbe-43fd-8146-92c1956a6e48.gif" height=240 alt="3D卡牌效果"/>
<img src="https://raw.githubusercontent.com/Ineloquent0/notes/main/images/688bcb20-4ebb-4de9-b3c7-fd7cf6353787.gif" height=240 alt="侦探镜效果"/>
<img src="https://raw.githubusercontent.com/Ineloquent0/notes/main/images/f41b20e2-2564-4569-a30c-7d70b6b27662.gif" height=240 alt="传送门效果"/>
<img src="https://raw.githubusercontent.com/Ineloquent0/notes/main/images/71794d66-dd32-4374-bbaf-8c2eeddc5f5b.gif" height=240 alt="侦探镜效果"/>
<img src="https://raw.githubusercontent.com/Ineloquent0/notes/main/images/00792bd1-b951-4f4e-9d9d-5e64d773c54d.gif" height=240 alt="笼中窥梦效果"/>
<img src="https://raw.githubusercontent.com/Ineloquent0/notes/main/images/29f13d23-90d8-4bed-86d8-6f506e2b6a21.gif" height=240 alt="笼中窥梦效果"/>
</div>

### 模板测试是什么

#### 从渲染管线出发

![渲染管线中的逐片元操作阶段](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/33535ecf-22ee-4eb1-99c2-609f40b2cb73.jpg)

渲染管线中的逐片元操作阶段：
* Pixel Ownership Test：像素所有权测试，控制对当前屏幕像素的使用权限，比如PIE时只对Scene窗口或者Game窗口有使用权限，其他窗口没有使用权限
* Scissor Test：裁剪测试，对可渲染内部部分内容的控制
* Alpha Test：透明度测试，只能实现不透明效果或全透明效果
* Stencil Test：模板测试
* Depth Test：深度测试
* Blending：混合操作，对不同像素的颜色混合

逐片元操作阶段是不可编程的，他是由渲染管线和硬件自身自我规定好的，但是我们可以对其中的内容进行配置（高度可配置）

#### 从逻辑上理解
通过一定条件来判断是对该片元或片元属性执行抛弃操作还是保留操作

![通过一定条件来判断是对该片元或片元属性执行抛弃操作还是保留操作](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/772d5164-f5b3-4675-a5c8-61b956e9a3e3.png)

* referenceValue：当前片元的模板缓冲的参考值
* stencilBufferValue：模板缓冲区里的值
* 中间的comparisonFunction，就是做一个比较

#### 从书面概念上理解

说到模板测试，就要先说到模板缓冲区。
* 模板缓冲区：与颜色缓冲区和深度缓冲区类似，模板缓冲区可以为屏幕上的每个像素点保存一个无符号整数值(通常的话是个8位整数)。这个值的具体意义视程序的具体应用而定。

* 模板测试：在渲染的过程中，可以用这个值与一个预先设定的参考值相比较，根据比较的结果来决定是否更新相应的像素点的颜色值。这个比较的过程被称为模板测试。模板测试发生在透明度测试（alpha test）之后，深度测试（depth test）之前。如果模板测试通过，则相应的像素点更新，否则不更新。


### 语法表示

``` hlsl
stencil {
    Ref referenceValue
    ReadMask readMask
    WriteMask writeMask
    Comp compareFunction
    Pass stencilOperation
    Fail stencilOperation
    ZFail stencilOperation
}
```

*  Ref：当前片元的参考值（0-255）
*  ReadMask：读掩码
*  WriteMask：写掩码
*  Comp：比较操作函数
*  Pass：测试通过，之后进行操作（StencilOperation，后边有详细讲解）
*  Fail：测试未通过，也会进行一个操作
*  ZFail：模板测试通过，深度测试未通过

#### 比较函数（Comparison Function）

语义    | 解释
---     |---
Greater | 相当于 “>” 操作，即仅当左边 > 右边，模板测试通过，渲染像素
GEqual  | 相当于 “>=” 操作，即仅当左边 >= 右边，模板测试通过，渲染像素
Less    | 相当于 “<” 操作，即仅当左边 < 右边，模板测试通过，渲染像素
LEqual  | 相当于 “<=” 操作，即仅当左边 <= 右边，模板测试通过，渲染像素
Equal   | 相当于 “=” 操作，即仅当左边 = 右边，模板测试通过，渲染像素
NotEqual| 相当于 “!=” 操作，即仅当左边！= 右边，模板测试通过，渲染像素
Always  | 不管公式两边为何值，模板测试总是通过，渲染像素
Never   | 不敢公式两边为何值，模板测试总是失败 ，像素被抛弃


#### 更新值

语义    | 解释
---     |---
Keep    | 保留当前缓冲中的内容，即 stencilBufferValue 不变
Zero    | 将 0 写入缓冲，即 stencilBufferValue 值变为 0
Replace | 将参考值写入缓冲，即将 referenceValue 赋值给 stencilBufferValue
IncrSat | stencilBufferValue 加 1，如果 stencilBufferValue 超过 255 了，那么保留为 255，即不大于 255
DecrSat | stencilBufferValue 减 1，如果 stencilBufferValue 超过为 0，那么保留为 0，即不小于 0
Invert  | 将当前模板缓冲值（stencilBufferValue）按位取反
IncrWrap| 当前缓冲的值加 1，如果缓冲值超过 255 了，那么变成 0，（然后继续自增）
DecrWrap| 当前缓冲的值减 1，如果缓冲值已经为 0，那么变成 255，（然后继续自减）


### 案例

<img src="https://raw.githubusercontent.com/Ineloquent0/notes/main/images/a1bef728-9bbe-43fd-8146-92c1956a6e48.gif" height=240 alt="3D卡牌效果"/>

#### 遮罩 Shader：
``` hlsl
Shader "Custom/StencilMask"
{
    Properties
    {
        // 蒙版ID
        _ID("Mask ID", Int) = 0
    }
    SubShader
    {
        // 渲染类型为不透明物体，队列为Geometry+1（在默认的不透明物体渲染队列之后）
        Tags { "RenderType"="Opaque" "Queue"="Geometry+1" }
        
        // 颜色遮罩，0表示不渲染任何颜色，也就是完全透明
        ColorMask 0 // RGBA、RGB、R、G、B、A、0
        // 关闭深度写入，防止后面要显示的东西被剔除
        ZWrite Off

        // 模板测试部分
        Stencil // input stencil
        {
            Ref [_ID] // 参考值
            Comp Always // 默认always
            Pass Replace // 默认keep
            // Fail Keep
            // ZFail Keep
        }

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
            };

            struct v2f
            {
                float4 pos : SV_POSITION;
            };

            v2f vert (appdata v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                // 随便给个颜色就行，因为颜色遮罩关闭了，不会输出任何颜色
                return fixed4(1, 1, 1, 1);
            }

            ENDCG
        }
    }
}

```

#### 物体 Shader：
``` hlsl
Shader "Custom/StencilObject"
{
    Properties
    {
        _Color ("Color", Color) = (1,1,1,1)
        _MainTex ("Albedo (RGB)", 2D) = "white" {}
        _Glossiness ("Smoothness", Range(0,1)) = 0.5
        _Metallic ("Metallic", Range(0,1)) = 0.0
        // 模板测试ID
        _ID("Mask ID", Int) = 0
    }
    SubShader
    {
        // 渲染队列在蒙版之后
        Tags { "RenderType"="Opaque" "Queue"="Geometry+2" }
        LOD 200

        Stencil
        {
            Ref [_ID] // 模板缓冲区的索引值
            Comp equal // 当给定的索引值和当前模板缓冲区的值相等时，才会渲染这个片元
        }

        CGPROGRAM
        #pragma surface surf Standard fullforwardshadows

        #pragma target 3.0

        sampler2D _MainTex;

        struct Input
        {
            float2 uv_MainTex;
        };

        half _Glossiness;
        half _Metallic;
        fixed4 _Color;

        UNITY_INSTANCING_BUFFER_START(Props)
        UNITY_INSTANCING_BUFFER_END(Props)

        void surf (Input IN, inout SurfaceOutputStandard o)
        {
            fixed4 c = tex2D (_MainTex, IN.uv_MainTex) * _Color;
            o.Albedo = c.rgb;
            o.Metallic = _Metallic;
            o.Smoothness = _Glossiness;
            o.Alpha = c.a;
        }
        ENDCG
    }
    FallBack "Diffuse"
}
```


### 总结
* 使用模板缓冲区最重要的两个值：当前模板缓冲值（stencil Buffer Value）和模板参考值（reference Value）
* 模板测试主要是对这两个值使用特定的比较操作：Never、Always、Less、LEqual、Greater、Equal
* 模板测试之后要对模板缓冲区的值（stencil Buffer Value）进行更新操作，更新操作包括：Keep、Zero、Replace、IncrSat、DecrSat、Invert 等等
* 模板测试之后可以根据结果对模板缓冲做不同的更新操作，比如模板测试成功操作 Pass，模板测试失败操作 Fail，深度测试失败操作 ZFail，还有正对正面和背面精确更新操作 PassBack，PassFront，FailBack 等等


### 扩展
掌握模板测试的核心思想，利用模板测试的特性同其他测试或图形算法结合使用

![模板测试扩展](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240929195259.jpg)



## 1.2 深度测试 (Depth Test)
深度测试主要用于处理物体的遮挡关系。

![深度测试](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240929195726.png)

### 1.2.1 深度测试是什么

#### 从渲染管线出发

在逐片元操作之中，模板测试之后，透明度混合之前进行的，同样是高度可配置的
其中在片元着色器之前还有一个 Early-Z 技术，利用了与深度测试相同的技术

#### 从逻辑上理解

``` hlsl
// 如果ZWrite On，且当前深度值和深度缓冲区的值作比较，如果通过就写入深度，不通过就忽略深度
if(Zwrite On && (correntDepthValue ComparisonFunction DepthBufferValue)){
		写入深度
}else{
		忽略深度
}
```
``` hlsl
// 当前深度值和深度缓冲区中的值做比较，如果通过就写入颜色缓冲区，不通过就不写入颜色缓冲区
if(correntDepthValue ComparisonFunction DepthBufferValue){
		写入深度缓冲
}else{
		不写入深度缓冲
}
```

#### 从书面概念上理解
所谓深度测试，就是针对当前对象在屏幕上（更准确的说是 frame buffer）对应的像素点，将对象自身的深度值与当前该像素点缓存的深度值进行比较，如果通过了，本对象在该像素点才会将颜色写入颜色缓冲区，否则否则不会写入颜色缓冲

#### 深度测试的发展

![深度测试的发展](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240929201206.png)

1. 控制渲染顺序
   * 画家算法：
      * 这里是指油画的画法，也就是画一幅油画，是从远处开始画，然后近处的东西一点点叠加在上面（GAMES系列的课提到过多次）
      * 存在的问题：例如一列物体，最前面的物体最大，站在正前面看只能看到最前面的物体，这样一来后边的就不用画了，不然就是性能浪费（OverDraw）。
   * Z-Buffer算法：通过深度缓冲区来控制渲染顺序
2. 控制Z-Buffer对深度的存储
   * 例如：什么时候更新深度缓冲区、什么时候使用深度缓冲区
   * 两个典型的功能： Z Test、Z Write
3. 控制不同类型物体的渲染顺序
   * 透明物体
   * 不透明物体
   * 渲染队列（很有用的概念，后边会讲）
4. 减少OverDraw
   * Early-Z，一种优化手段，后边会讲
      * Z-cull（优化手段）
      * Z-check（确认正确遮挡关系）


### 1.2.2 深度缓冲区（Z-Buffer）

* 深度缓冲就像颜色缓冲（储存所有的片段颜色：视觉输出）一样，在每个片段中储存了信息，并且（通常）和颜色缓冲有着一样的宽度和高度。
* 深度缓冲是由窗口系统自动创建的，它会以16、24或32位float的形式储存它的深度值。在大部分的系统中，深度缓冲的精度都是24位的
* **z-buffer中存储的是当前的深度信息，对于每个像素存储一个深度值**
* 我们可以通过Z-Write 、Z-Test来调用Z-Buffer，来达到想要的渲染效果

### 1.2.3 深度写入（ZWrite）
深度写入包括两种状态：ZWrite On 与 ZWrite Off

当我们开启深度写入的时候，物体被渲染时针对物体在屏幕（更准确地说是frame buffer）上每个像素的深度都写入到深度缓冲区；反之，如果是 ZWrite Off，那么物体的深度就不会写入深度缓冲区。

但是，物体是否会写入深度，除了 ZWrite 这个状态之外，更重要的是需要深度测试通过，也就是 ZTest 通过，如果 ZTest 都没通过，那么也就不会写入深度了

ZTest 分为通过和不通过两种情况，ZWrite 分为开启和关闭两种情况的话，一共就是四种情况：
1. 深度测试通过，深度写入开启：写入深度缓冲区，写入颜色缓冲区；
2. 深度测试通过，深度写入关闭：不写深度缓冲区，写入颜色缓冲区；
3. 深度测试失败，深度写入开启：不写深度缓冲区，不写颜色缓冲区；
4. 深度测试失败，深度写入关闭：不写深度缓冲区，不写颜色缓冲区；


### 1.2.4 深度测试（ZTest）

#### 比较操作

ZTest 状态  | 描述
---         | ---
Greater     | 深度大于当前缓存则通过
LEqual      | 深度小于等于当前缓存则通过
Less        | 深度小于当前缓存则通过
GEqual      | 深度大于等于当前缓存则通过
Equal       | 深度等于当前缓存则通过
NotEqual    | 深度不等于当前缓存则通过
Always（Off）| 深度不论如何都通过
Never       | 深度不论如何都不通过

**默认是ZWrite On 和ZTest Lequal，深度缓存一开始为无穷大**


### 1.2.5 渲染队列
Unity中内置的渲染队列，按照渲染顺序，从先往后进行排序，**队列数越小的越先渲染，队列数越大的越后渲染**
* BackGround（1000）：最早被渲染的物体的队列。
* Geometry（2000）：不透明物体的渲染队列。大多数物体都应该使用该队列进行渲染，也是Unity默认的渲染队列。
* AlphaTest（2450）：有透明通道，需要进行 Alpha Test 的物体的队列，比在 Geometry 中更有效。
* Transparent（3000）：半透明物体的渲染队列。一般是不写入深度的物体，Alpha Blend 等的物体在该队列中渲染。
* Overlay（4000）：最后被渲染的物体的队列，一般是覆盖效果，比如镜头光晕，屏幕贴片之类的。

在Unity中设置渲染队列：`Tags { “Queue” = “Transparent”}`，默认是 Geometry

* **不透明物体的渲染顺序：从前往后**
* **透明物体的渲染顺序：从后往前（OverDraw）**

可以在 shader 的 Inspector 面板查看相关属性

![在 shader 的 Inspector 面板查看相关属性](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240930121734.png)


### 1.2.6 Early-Z 技术
传统的渲染管线中，ZTest其实是在Blending阶段，这时候进行深度测试，所有对象的像素着色器都会计算一遍，没有什么性能提升，仅仅是为了得出正确的遮挡结果，会造成大量的无用计算，因为每个像素点上肯定重叠了很多计算。
因此 **现代 GPU 中运用了Early-Z的技术，在 Vertex 阶段和 Fragment 阶段之间（光栅化之后，fragment 之前）进行一次深度测试**，如果深度测试失败，就不必进行 fragment 阶段的计算了，因此在性能上会有很大的提升。但是最终的 ZTest 仍然需要进行，以保证最终的遮挡关系结果正确。

前面的一次主要是 Z-Cull 为了裁剪以达到优化的目的，后一次主要是 Z-Check，为了检查，如下图：

![3e2d4fed-438e-4b20-b608-8404d5f61c7f](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/3e2d4fed-438e-4b20-b608-8404d5f61c7f.jpg)
![0d609c6a-65c7-46ef-a69b-c57875effe1e](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/0d609c6a-65c7-46ef-a69b-c57875effe1e.png)


### 1.2.7 深度值

模型在渲染管线中的几次空间变换

![深度值](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20240930123132.jpg)

* 模型一开始所在的模型空间：无深度。
* 通过M矩阵变换到世界空间，此时模型坐标已经变换到了齐次坐标（x，y，z，w）：深度存在z分量。
* 通过V矩阵变换到观察空间（摄像机空间）：深度存在z分量（线性）
* 通过P矩阵变换到裁剪空间：深度缓冲中此空间的z/w中（已经变成了非线性的深度）
* 最后通过一些投影映射变换到屏幕空间 


#### 为什么深度缓冲区中要存储一个非线性的深度？
参考链接：https://learnopengl-cn.readthedocs.io/zh/latest/04%20Advanced%20OpenGL/01%20Depth%20testing/

1.给近处更多的精度

* 在精度有限的深度缓冲区中合理分配存储到精度，**较近区域的深度信息分配更高的精度，较远区域的深度信息分配更低的精度**（和gamma映射异曲同工）

* 正确的投影特性的非线性深度方程是和 1/Z 成正比的，这样基本做的是在Z很近的时候高精度和Z很远的时候低精度。例如 1.0 和 2.0 之间的 z 值，将变为 1.0 到 0.5 之间，这样在 z 很小的时候给了我们很高的精度，而 50.0 和 100.0 之间的 z 值将只占 2% 的浮点数的精度，这正是我们想要的。


2.防止深度冲突

* **深度值的精度不足可能会带来深度冲突的问题**。所以在深度缓冲区中使用非线性深度，在近处用更高精度的数据存储，可以有效减少近处的深度冲突；而远处的因为不容易被看见，即便发生深度冲突也不会造成太多影响

* 深度冲突无法避免，但是有技巧可以防止出现：
    * 让物体之间不要离得太近
    * 尽可能把近平面设置得远一些，可以有效在椎体中提高精度
    * 牺牲性能，使用更高精度的深度值（24→32位）



### 案例

* 先对渲染队列进行排列，再在同一个渲染队列中按深度进行排序
* 对于多pass shader，unity会从所以pass的所有的queue中挑选最靠前的，然后把这个物体放到一个队列中，然后他会根据pass的顺序逐pass执行

X-Ray Shader：
``` hlsl
Shader "Custom/XRay"
{
    Properties
    {
        _MainTex ("Texture", 2D) = "white" {}
        _XRayColor ("XRay Color", Color) = (1,1,1,1)
    }
    SubShader
    {
        // XRay Pass
        Pass
        {
            Tags { "RenderType"="Transparent" "Queue"="Transparent" }
            Blend SrcAlpha One
            ZTest Greater // 核心
            ZWrite Off
            Cull Back

            CGPROGRAM
            #include "UnityCG.cginc"

            fixed4 _XRayColor;

            struct appdata
            {
                float4 vertex : POSITION;
                float3 normal : NORMAL;
            };

            struct v2f
            {
                float4 vertex : SV_POSITION;
                fixed4 color : TEXCOORD0;
            };

            v2f vert(appdata v)
            {
                v2f o;

                o.vertex = UnityObjectToClipPos(v.vertex);

                float3 normal = normalize(v.normal);
                float3 viewDir = normalize(ObjSpaceViewDir(o.vertex));
                fixed fresnel = 1 - dot(normal, viewDir);

                o.color = _XRayColor * fresnel;

                return o;
            }

            fixed4 frag(v2f i) : SV_Target
            {
                return i.color;
            }

            #pragma vertex vert
            #pragma fragment frag

            ENDCG
        }


        // 正常 Pass
        Pass
        {
            Tags { "RenderType"="Opaque" }
            ZTest LEqual
            ZWrite On

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
                float2 uv : TEXCOORD0;
            };

            struct v2f
            {
                float2 uv : TEXCOORD0;
                float4 vertex : SV_POSITION;
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                fixed4 col = tex2D(_MainTex, i.uv);
                return col;
            }
            ENDCG
        }
    }
}
```

### 总结
* 使用深度缓冲区最重要的两个值：当前深度缓冲值 ZBufferValue 和深度参考值 referenceValue，并通过比较操作获取理想的渲染效果
* Unity 中的渲染顺序：先渲染不透明物体，顺序是从前往后；再渲染透明物体，顺序是从后到前
* 通过 ZWrite 和 ZTest 组合使用控制半透明物体的渲染
* 引入 Early-Z 技术后的深度测试相关渲染流程
* 深度缓冲区中存储的深度值为0到1范围的浮点值，且为非线性

### 扩展

![深度测试扩展](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20241010192122.jpg)

### 参考资料
https://blog.csdn.net/puppet_master/article/details/53900568
https://learnopengl-cn.readthedocs.io/zh/latest/04%20Advanced%20OpenGL/01%20Depth%20testing/
https://docs.unity3d.com/cn/2018.4/Manual/SL-CullAndDepth.html
https://blog.csdn.net/yangxuan0261/article/details/79725466
《Unity ShaderLab 开发实战详解》
《Unity Shader 入门精要》
https://roystan.net/articles/toon-water.html


------------------------------------------------------------------------


# 3. 曲面细分与几何着色器

## 3.1 应用

### 曲面细分着色器的应用

相较于直接增加模型的面数，曲面细分着色器可以在游戏进行时，根据自定义的规则（距离等），动态调整模型的复杂度，可以带来更好的性能。

![曲面细分应用](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20241017105058.jpg)

### 几何着色器的应用

* 几何动画

<div align=center>
<img src="https://raw.githubusercontent.com/Ineloquent0/notes/main/images/4cf6b995-469a-40ff-baf6-518a247da6be.gif" alt="几何动画"/>
<img src="https://raw.githubusercontent.com/Ineloquent0/notes/main/images/80e0933d-9cbe-4826-8ca7-0ff280496339.gif" alt="几何动画"/>
</div>

* 草地（与曲面细分着色器结合，动态调整草地的疏密）

![草地](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20241017105744.jpg)


## 3.2 着色器执行顺序

![着色器执行顺序](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/2bb24a05-c801-4cf0-8809-b377c2fb2ad7.png)

整体顺序：顶点 → 曲面细分 → 几何 → 片元

曲面细分又分为：Hull shader 、Tessellation Primitive Generator 、 Domain shader
* Hull Shader：可编程，定义细分的参数
* Tessellation Primitive Generator：不可编程不可控制
* Domain Shader：曲面细分得到的点是在重心空间的，需要在Domain Shader中将点转换到我们要用的空间中



## 3.3 TESS

### TESS 的输入与输出
**输入**：Patch，可以看成是多个顶点的集合，包含每个顶点的属性，可以指定一个 Patch 包含的顶点数以及自己的属性
**功能**：将图元细分(可以是三角形，矩形等)
**输出**：细分后的顶点

### TESS 流程
* HULL Shader
    1. 决定细分的数量(设定 Tessellation factor 以及 Inside Tessellation factor)
    2. 对输入的 Patch 参数进行改变(如果需要)
* Tessellation Primitive Generation：进行细分操作
* Domain Shader：对细分后的点进行处理，从重心空间(Barycentric coordinate system)转换到屏幕空间


### HULL Shader 各参数解析

参考：https://zhuanlan.zhihu.com/p/94646062

**Tessellation Factor**：决定将一条边分成几部分
* **equal_Spacing**：均匀分割，每一部分的长度相同
    ![equal_Spacing](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/equal_Spacing.gif)
* **fractional_even_spacing**：段数为向上取最近的偶数，最小值是2，会把周长分为n-2的等长部分、以及两端不等长的部分（两端部分和小数有关）
    ![fractional_even_spacing](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/fractional_even_spacing.gif)
* **fractional_odd_spacing**：段数为向上取最近的奇数，最小值是1，会把周长分为n-2的等长部分、以及两端不等长的部分（两端部分和小数有关）
    ![fractional_odd_spacing](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/fractional_odd_spacing.gif)


**Inner Tessellation Factor**：控制内部图形的切割方式

![图形内部切割方式](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20241017113639.png)


## 3.4 GS
#### 输入与输出
**输入**：图元（三角形、矩形、线等），据图元的不同，shader 中会出现对应不同数量的顶点
**输出**：输出同样为图元，一个或多个，需要自己从定点构建，顺序很重要；同时需要定义最大输出的顶点数


------------------------------------------------------------------------------------


# 4. 延迟渲染原理介绍

## 4.1 渲染路径
渲染路径是决定光照的实现方式，简而言之就是当前渲染目标使用光照的流程

## 4.2 渲染方式

前向渲染和延迟渲染对比

![前向渲染和延迟渲染对比](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20241123190425.jpg)

### 4.2.1 前向渲染（Forward Rendering）

**流程**：待渲染几何体 → 顶点着色器 → 片元着色器 → 渲染目标

在渲染每一帧时，**每个顶点/片元都要执行一次片元着色器代码**，这时需要将所有的光照信息都传递到片元着色器中。
虽然大部分情况下的光源都趋向于小型化，而其照亮的区域也不大，但即便是光源离这个像素所对应的世界空间中的位置很远，但计算光照时，还是会把所有的光源都考虑进去。
例如，物体受n个光源影响，那么在每一个片元执行着色器代码时，都必须吧这n个光源都传递进着色器中执行光照计算。

伪代码：

```
For Each light:
	For each object affected by light:
		framebuffer += object * light
```

### 4.2.2 延迟渲染（Deferred Rendering）

延迟渲染是主要**解决大量光照渲染的方案**。

延迟渲染的实质是先不要做迭代三角形做光照计算，而是**先找出来你能看到的所有像素，再去迭代光照**。直接迭代三角形的话，由于大量三角形你是看不到的，无疑是极大的浪费。

**流程**：待渲染几何体 → 顶点着色器 → MRT → 光照计算 → 渲染目标

**将渲染过程拆分成两个渲染通路（pass）**：
* 第一个 pass 称为**几何处理通路**。首先将场景渲染一次，获取到待渲染对象的各种几何信息存储到名为 G-buffer 的缓冲区中，这些缓冲区将会在之后用作更复杂的光照计算。由于有深度测试，所以最终写入 G-buffer 中的各个数据都是离摄像机最近的片元的几何属性，这意味着最后在 G-buffer 中的片元必定要进行光照计算的。
* 第二个 pass 称为**光照处理通路**。该 pass 会遍历所有 G-buffer 中的位置、颜色、法线等参数，执行一次光照计算。

> * 实际上 Fragment Shader 也是输出了片元颜色的，只不过没有进行光照计算
> * 延迟渲染无法支持半透明物体的渲染，在延迟渲染管线下渲染半透明物体，只能是在延迟渲染处理完成之后，最后再用前向渲染的方式去渲染半透明物体
> * G-Buffer 中的数据都是 2D 的，所以最终的光照计算相当于 2D 的光照后处理

伪代码：

```
For each object:
    Render to multiple targets
For each light:
    Apply light as a 2D postprocess
```


### 4.2.3 不同渲染路径的优劣及特性

#### 不同渲染路径的特性
1. **后处理方式不同**：如果需要深度信息来进行后处理，前向渲染就需要单独渲染出一张深度图，而延迟渲染直接从 G-Buffer 中拿深度图即可
2. **着色计算不同(shader)**：由于延迟渲染光照计算统一是在 LightPass 中完成的，所以只能计算一个光照模型。如果需要其他的光照模型，只能切换 Pass
3. **抗锯齿方式不同**

#### 不同渲染路径的优劣

**前向渲染**
* 优点：
    * 支持半透明渲染
    * 支持使用多个光照 pass
    * 支持自定义光照计算方式
* 缺点：
    * 光源数量对计算复杂度影响巨大
    * 访问深度等数据需要额外计算

**延迟渲染**
* 优点：
    * 大量光照场景优势明显
    * 只渲染可见像素，节省计算量
    * 对后处理支持良好
    * 用更少的 shader
* 缺点：
    * 对MSAA支持不友好
    * 透明物体渲染存在问题
    * 占用大量的显存带宽


## 4.3 其他

#### 渲染路径设置

![渲染路径设置](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20241123210004.png)

#### 移动端优化

两个 TBDR 的优化方式
* SIGGRAPH2010 上提出的，通过分块来解决降低带宽内存用量
    ![dde48c5b-f787-4dc1-bcc6-21bd36f7ac57](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/dde48c5b-f787-4dc1-bcc6-21bd36f7ac57.png)
* PowerVR 基于手机 GPU 的 TBR 架构提出的，通过 HSR 减少 overdraw（做一些可见性测试）
    ![15d7f7b0-4c90-448e-8a11-4ae1d0b305ed](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/15d7f7b0-4c90-448e-8a11-4ae1d0b305ed.png)

#### 其他渲染路径
参考资料：https://zhuanlan.zhihu.com/p/54694743

* 延迟光照 （Light Pre-Pass / Deferred Lighting）
    减少 G-buffer 占用的过多开销，支持多种光照模型
    ![8ffc1ba4-9df1-4b82-919f-f62e1d0a6245](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/8ffc1ba4-9df1-4b82-919f-f62e1d0a6245.png)

* Forward+（即Tiled Forward Rendering，分块正向渲染）
    减少带宽，支持多光源，强制需要一个preZ
    ![6e723b53-eeaa-4b76-86f9-4b65073e64d0](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/6e723b53-eeaa-4b76-86f9-4b65073e64d0.png)

* 群组渲染 Clustered Rendering
    带宽相对减少，多光源下效率提升

#### MSAA 与延迟渲染的不兼容
MSAA 在延迟渲染中的问题是：像素在进行光照计算前已经被光栅化了，MSAA 需要基于 sub-pixel 数据进行处理，光栅化后的每个像素的 sub-pixel 们是一样的，处理无效，所以没有办法用精度更高的像素来进行渲染。

![https://catlikecoding.com/unity/tutorials/rendering/part-13/](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/2c5c3413-e11d-403c-af8f-441345cb4e8d.png)

参考资料：
https://docs.microsoft.com/en-us/windows/win32/direct3d9/multiple-render-targets
https://docs.nvidia.com/gameworks/index.html#gameworkslibrary/graphicssamples/d3d_samples/antialiaseddeferredrendering.htm


#### 不同path下光源shader编写

![不同path下光源shader编写](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/2f79032b-14fb-430e-adc8-d0250022b9f1.png)


#### PreZ/Zprepass

实际上就是一个深度计算，与深度图一样都是用一个 pass 去计算深度。PreZ 与深度图的区别在于，深度图是将深度信息绘制到了一张 RT 上，为了记录数据并方便进行数据间的传输；而 PreZ/Zprepass 是硬件自动进行的，只是计算深度然后在 shader 中使用（做屏幕等距边缘光的时候用到了），或者透明排序的时候会涉及三段深度排序的方式，来让透明的物体深度正确。

如果在 shader 中要用到 DepthOnlyPass 获取到的深度图的话，需要勾选 Depth Texture 的生成 —— 屏幕空间等距边缘光

![12d67842-10a4-4882-b30c-819e06368e45](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/12d67842-10a4-4882-b30c-819e06368e45.png)


### 参考资料
https://zhuanlan.zhihu.com/p/28489928
https://zhuanlan.zhihu.com/p/259760974
https://www.3dgep.com/forward-plus/#Forward
https://gamedevelopment.tutsplus.com/articles/forward-rendering-vs-deferred-rendering--gamedev-12342



----------------------------------------------------------------------------


# 5. Early-Z 和 Z-Prepass

## 5.1 深度测试（Depth Test）

#### 传统渲染管线中的深度测试

![传统渲染管线中的深度测试](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20241129205030.png)


#### 为什么做深度测试

深度测试可以解决：物体的可见遮挡性问题

<div align=center>
<img src="https://raw.githubusercontent.com/Ineloquent0/notes/main/images/8e2ac7c7-0090-4915-848e-501a37b22366.png" alt="8e2ac7c7-0090-4915-848e-501a37b22366" />
<img src="https://raw.githubusercontent.com/Ineloquent0/notes/main/images/858c5a79-6782-45d5-bb46-449a9acfcf2d.png" alt="858c5a79-6782-45d5-bb46-449a9acfcf2d" />
</div>

深度缓冲区默认值为∞，当渲染一个物体时，它与深度缓冲区中的值做比较，如果它的值符合深度测试(ZTest)条件，那么它才会被渲染，否则它就被丢弃。

例如上图：
* 首先先渲染紫色三角形，紫色三角形的深度值为5，当渲染它的时候，它与深度缓冲区中的∞做比较，因为默认的test比较条件为LEqual，所以5小于∞，并且写入了深度缓冲器
* 之后进行黄色三角形的渲染，和上一步同理，会进行深度对比并进行相关操作，渲染完成后的结果如图中右下角所示


#### 深度测试流程

![深度测试流程](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/c9ac637e-6930-4aa2-8f64-f66d2d9256ca.png)


#### 伪代码

```
for(each triangle T){                       //对每一个三角形	
    for(each fragment(x,y,z) in T){	        //对每一个三角形中的片元    	
        if(fragment.z < ZBuffer[x,y]){	    //深度测试：如果片元深度小于ZBuffer深度       
            FrameBuffer[x,y] = fragment.rgb;//更新颜色            
            ZBuffer[x,y] = fragment.z;		//更新深度        
        }        
        else{					            //深度测试失败        	
            discard;					    //什么都不做，片元数据被丢弃        
        }    
    }
}
```

#### 深度测试带来的问题

![深度测试带来的问题](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/5516bc50-3d4a-44d6-b8b6-422df48e38e9.png)

物体2与物体3会被物体1遮挡，所以它们计算得到的片元都将会被丢弃，会造成性能问题。

这个问题的解决方法就是：early-z


## 5.2 提前深度测试 Early-Z

在传统管线中的光栅化阶段之后、片元着色器之前加一步操作，如果没有通过深度测试的物体就直接舍弃，不进行后续计算。

![提前深度测试](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20241129211916.png)

![6570d5ae-d23b-458e-b2b0-6f27159b223b](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/6570d5ae-d23b-458e-b2b0-6f27159b223b.png)

在Early-Z先计算了物体1的片元情况下，物体2与物体3将不会进行片元计算

补充：Early-Z同样可以搭配使用模板测试

### Early-Z 失效的情况

1. 开启 Alpha Test 或 clip/discard 等手动丢弃片元操作
2. 手动修改 GPU 插值得到的深度
3. 开启 Alpha Blend
4. 关闭深度测试 Depth Test


### 高效利用Early-Z

![深度测试带来的问题](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/5516bc50-3d4a-44d6-b8b6-422df48e38e9.png)

如果按照 3-2-1 顺序渲染测试，Early-Z 将不会带来任何优化效果

在渲染前，将不透明物体从近往远渲染的话，Early-Z能发挥最大的性能优化
具体怎么排序？ 
* 可以让 cpu 将物体按照由近到远的顺序排好，再交付给 gpu 进行渲染

问题：
* 复杂的场景，cpu性能消耗很大
* 严格按照由近到远的顺序渲染，将不能同时搭配批处理优化手段。

解决方法：Z-Prepass


## 5.3 Z-Prepass

### 方式1：双Pass法
使用两个Pass：
* 在第一个 Pass 即 Z-Prepass 中仅仅只写入深度，不计算输出任何颜色
* 在第二个 Pass 中关闭深度写入，并且将深度比较函数设置为相等。

效果：每个物体都会渲染两个 Pass，且所有物体的 Z-Prepass 的结果就自动形成了一个最小深度值的缓冲区 Z-buffer，无需 CPU 进行排序

``` hlsl
Shader "Custom/EarlyZPresentation" {
    Properties { //.......}
    SubShader {
        Tags { "RenderType"="Opaque" }
        
        //Z-Prepass部分
        Pass
        {
            ZWrite On	//开启深度写入
            ColorMask 0	//关闭颜色输出

            CGPROGRAM
            //...这里省略了单纯地顶点变换计算部分
            ENDCG
        }

        //正常地计算输出颜色的Pass
        Pass
        {
            Zwrite Off	//关闭深度写入
            ZTest Equal	//深度相等为通过

            CGPROGRAM
            //...这里省略了顶点变换和颜色等计算部分
            ENDCG
        }
    }
}
```

#### 问题
* 问题1：动态批处理。多 Pass Shader 无法进行动态批处理 --> Draw Call 问题
* 问题2：Draw Call。使用 Z-Prepass Shader 的物体，Draw Call 会多一倍

![aPr4NF1sP0bn5oL0xVo_0g](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/aPr4NF1sP0bn5oL0xVo_0g.png)


### 方式2：提前分离的 PrePass

仍然使用两个Pass，但：
* 将原先第一个 Pass（即 Z-Prepass）单独分离出来为单独一个 Shader，并先使用这个 Shader 将整个场景的 Opaque 的物体渲染一遍。
* 而原先材质只剩下原先的第二个 Pass，仍然关闭深度写入，并且将深度比较函数设置为相等。

![20241129215839](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20241129215839.png)


### Z-Prepass 也是透明渲染的一种解决方案

![Z-Prepass也是透明渲染的一种解决方案](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20241129220105.png)


### Z-Prepass 的其他问题

Z-prepass的性能消耗是否能被忽视

国外论坛一位名为lipsryme的老哥做了一项实验：

![4a110aba-e3a3-46d5-a2d9-f04a041d65cf](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/4a110aba-e3a3-46d5-a2d9-f04a041d65cf.png)

* 可以看到，Z-prepass的消耗为2.0ms，而带来的优化只减少了0.3ms（2.7-2.4）
* Z-prepass 是需要根据项目的实际情况来决定是否采用的。

总结有以下建议：
* 当一个有非常多 OverDraw 的场景，且不能很好的将不透明物体从前往后进行排序时，可以考虑使用 Z-prepass 进行优化
* 注意，Z-prepass 会增加 DrawCall，不恰当的使用可能是负优化


## 5.4 Early-Z 和 Z-Prepass的实例应用

### 面片叠加的头发渲染
对于半透明的面片来说，需要从后往前进行排序渲染才能得到正确的透明度混合结果

![面片叠加的头发渲染](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/20241129222109.png)

### 排序后的头发渲染

![排序后的头发渲染](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/b2a9d1bf-307c-43f1-a8dc-ce6ce951c4ac.png)

Pass 1 - 不透明部分
- 启用 alpha test，仅通过不透明像素
- 禁用背面剔除
- 启用 Z writes，将 Z test 设置为 Less

Pass 2 - 透明背面部分
- 启用 alpha test，通过所有非不透明像素
- 剔除正面多边形
- 禁用 Z writes，将 Z test 设置为 Less

Pass 3 - 透明正面部分
- 启用 alpha test，通过所有非不透明像素
- 剔除背面多边形
- 启用 Z Write，将 Z test 设置为 Less

### 性能改善

![性能改善](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/9d54edcc-828b-4c70-810c-af7549f85be0.png)

使用 Early-Z 剔除
透明度测试开启时 Early-Z 无法使用的解决方案：    
* 使用一个简单的 shader 进行透明度测试形成 Z-Buffer（就是我们上边说的提前分离的Z-Prepass）


### 改善的渲染方案

![改善的渲染方案](https://raw.githubusercontent.com/Ineloquent0/notes/main/images/f5829ba1-19a7-4b5a-82c7-f7d25518a268.png)

Pass 1：准备 Z-Buffer
  * 开启透明度测试
  * 关闭背面剔除
  * 开启深度写入，深度测试设置为less
  * 关闭颜色缓冲区写入
  * 用于一个简单的片元着色器来返回透明度值

Pass 2、Pass 3、Pass 4 参考之前排序后的头发渲染部分，同理


## 5.5 参考资料
https://www.cnblogs.com/ghl_carmack/p/10166291.html 	---深入剖析GPU Early Z优化














