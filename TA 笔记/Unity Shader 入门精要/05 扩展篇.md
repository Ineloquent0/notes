扩展篇旨在进一步扩展读者的视野。本篇将会介绍 Unity 的表面着色器的实现机制，并介绍基于物理渲染的相关内容。最后，我们给出了更多的关于学习渲染的资料。



# 第17章 Unity 的表面着色器探秘

2009年，Unity 的渲染工程师 Aras 连续发表3篇名为《Shaders must die》的博客。博客中，Aras 认为把渲染流程分为顶点和像素的抽象层面是不易理解的，他提出应该划分为：表面着色器、光照模型和光照着色器这样的层面。

* 表面着色器：定义模型表面的反射率、法线和高光等；
* 光照模型：选择使用兰伯特还是 Blinn-Phong 等模型；
* 光照着色器：计算光照衰减、阴影等。

这样开发者只需要和表面着色器打交道，如：混合纹理、颜色等；而光照模型是可以提前定义好的，只需要选择几种预定义的光照模型即可；光照着色器一旦由系统实现后，更不会轻易改动。这样大大减轻 Shader 编写者的工作量。
2010 年的 Unity 3 中，Surface Shader 被加入到 Unity 中。

表面着色器本质还是顶点/片元着色器，只是中间的转化过程由 Unity 来实现了，开发者只需要考虑纹理、颜色，使用法线纹理填充表面法线，指定光照模型，其他的统统不用考虑。



## 17.1 表面着色器的一个例子

新建 Shader 命名为 Chapter17-BumpedDiffuse ，我们将实现一个使用了法线纹理的漫反射效果，可以参考 Unity 内置的 "Legacy Shaders/Bumped Diffuse" ，打开 Chapater17-BumpedDiffuse 粘贴以下代码：

``` hlsl
Shader "Unity Shaders Book/Chapter 17/Bumped Diffuse" {
    Properties {
        _Color ("Color", Color) = (1,1,1,1)
        _MainTex ("Base (RGB)", 2D) = "white" {}
        _BumpMap ("Normal Map", 2D) = "bump" {}
    }

    SubShader {
        Tags { "RenderType"="Opaque" }
        // 设置了表面着色器的 LOD 值
        LOD 300

        // CG 代码直接且必须写在 SubShader 块中，Unity 会在背后为我们生成多个 Pass
        CGPROGRAM
        // 表面着色器编译指令，指明表面函数和光照模型
        #pragma surface surf Lambert
        #pragma target 3.0

        sampler2D _MainTex;
        sampler2D _BumpMap;
        fixed4 _Color;

        struct Input {
            float2 uv_MainTex;
            float2 uv_BumpMap;
        };

        void surf (Input IN, inout SurfaceOutput o) {
            fixed4 c = tex2D (_MainTex, IN.uv_MainTex) * _Color;
            o.Albedo = c.rgb;
            o.Alpha = c.a;
            o.Normal = UnpackNormal(tex2D(_BumpMap, IN.uv_BumpMap));
        }
        ENDCG
    }
    FallBack "Diffuse"
}
```

从上面的例子可以看出，表面着色器代码量很少，我们可以非常轻松地实现常见的光照模型，甚至不需要和任何光照变量打交道，Unity 就帮我们处理好了每个光源的光照结果。

读者可以在 [Unity 官方手册](https://docs.unity3d.com/Manual/SL-SurfaceShaders.html) 中找到更多关于 Unity 表面着色器的资料。

表面着色器最重要的部分是：
* **两个结构体**：是表面着色器中不同函数之间信息传递的桥梁。
* **编译指令**：是我们和 Unity 沟通的重要手段。


## 17.2 编译指令

编译指令是我们和 Unity 沟通的重要方式，通过它可以告诉 Unity:“嘿，用这个表面函数设置表面属性，用这个光照模型模拟光照，我不要阴影和环境光，不要雾效！”

编译指令最重要的作用是指明该表面着色器使用的 **表面函数** 和 **光照函数**，并设置一些可选参表面着色器的 CG 块中的第一句代码往往就是它的编译指令。编译指令的一般格式如下：
``` hlsl
#pragma surface surfaceFunction lightModel [optionalparams]
```
其中，`#pragma surface` 用于指明该编译指令是用于定义表面着色器的，在它的后面需要指明使用的表面函数（surfaceFunction）和光照模型（lightModel），同时，还可以使用一些可选参数来控制表面着色器的一些行为。


### 17.2.1 表面函数

函数格式：

``` hlsl
void surf(Input IN, inout SurfaceOutput o)
void surf(Input IN, inout SurfaceOutputStandard o)
void surf(Input IN, inout SurfaceOutputStandardSpecular o)
```
其中，后两个是 Unity 5 中由于引入了基于物理的渲染而新添加的两种结构体。
SurfaceOutput、SurfaceOutputStandard、SurfaceOutputStandardSpecular 是 Unity 内置的结构体，它们需要配合不同的光照模型使用。

在表面函数中，会使用输入结构体 Input IN 来设置各种表面属性，并把这些属性存储在输出结构体 SurfaceOutput o 中，在传递给光照函数计算光照结果。


### 17.2.2 光照函数














